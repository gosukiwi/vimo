" [ FILES ]
" File-related functionality. See also `rcplugins/ctrlp` and
" `rcplugins/filebeagle`.
"
" Key bindings
"
"     <leader>fr
"       File Rename
"       NORMAL MODE
"
"     <leader>fd
"       File Delete
"       NORMAL MODE
"
"     <C-s>
"       Save current file
"       NORMAL MODE / INSERT MODE
"

" save with <C-s>
nnoremap <C-s> :w<CR>
inoremap <C-s> <Esc>:w<CR>a

" backup and swap files are saved in ~/.vimo/tmp
set backupdir=~/.vimo/tmp
set directory=~/.vimo/tmp

function RenameFile()
  let fileDirectory = expand('%:h')
  call inputsave()
  let newName = input("New file name: " . fileDirectory . "/")
  call inputrestore()

  if "" == newName
    return
  endif

  let oldFileName = expand('%')
  execute "saveas " . fileDirectory . "/" . escape(newName, " ")
  bd# | call delete(oldFileName) | e " re-runs autocommands in case filetype changed
endfunction

function DeleteFile()
  call delete(expand('%')) | bdelete!
endfunction

nnoremap <leader>fr :call RenameFile()<CR><CR>
nnoremap <leader>fd :call DeleteFile()<CR><CR>

" vim:ft=vim
